plugins {
    id 'java-library'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    implementation project(':source:er2dbc')

    implementation platform('io.projectreactor:reactor-bom:Dysprosium-SR1')
    implementation platform('org.springframework.boot.experimental:spring-boot-bom-r2dbc:0.1.0.M3')

    implementation 'org.springframework.boot:spring-boot:2.2.4.RELEASE'
    implementation 'io.projectreactor:reactor-core'
    implementation 'io.projectreactor:reactor-tools'
    implementation 'org.springframework.data:spring-data-commons:2.2.4.RELEASE'
    implementation 'org.springframework.data:spring-data-relational:1.1.3.RELEASE'
    implementation 'org.springframework.boot.experimental:spring-boot-starter-data-r2dbc'

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.boot.experimental:spring-boot-test-autoconfigure-r2dbc'
    testImplementation 'io.projectreactor:reactor-test'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'ModMappings-Repositories'
                description = 'Reactive R2DBC based repositories used during ModMapping operations.'
                url = 'https://modmappings.org'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://github.com/ModMappings/ManagementSystem/blob/master/LICENSE'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ModMappings/ManagementSystem.git'
                    developerConnection = 'scm:git:ssh://github.com/ModMappings/ManagementSystem.git'
                    url = 'https://github.com/ModMappings/ManagementSystem.git'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.properties.mmmsMavenUser ?: 'fake'
                password project.properties.mmmsMavenPassword ?: 'news'
            }
            url 'https://nexus.minecraftforge.net/repository/modmappings/'
        }
    }
}